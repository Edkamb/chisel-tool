module NTank;

data HybridSpec =  ObjInv(String) | Ensures(String) | Requires(String) | TimedRequires(String);

type Real = Rat;

[HybridSpec: Requires("4 < inVal & inVal < 9")]
class CSingleTank(Real inVal){

[HybridSpec: ObjInv("3 <= level & level <= 10")]
    physical{
        Real level = inVal : level' = drain;
        Real drain = -1/2 : drain' = 0;
    }

    Unit run(){ this!ctrl(); }

    Unit ctrl(){
        await diff level <= 3 & drain <= 0 | level >= 10 & drain >= 0;
        if(level <= 3) drain =  1/2;
        else           drain = -1/2;
        this!ctrl();
    }

    Real getDrain(){
        return drain;
    }
}

//for zeno
[HybridSpec: Requires("4 < inVal & inVal < 9")]
class CNonZenoTank(Real inVal){

[HybridSpec: ObjInv("3 <= level & level <= 10")]
    physical{
        Real level = inVal : level' = drain;
        Real drain = -1/2 : drain' = 0;
    }

    Unit run(){ this!ctrl(); this!ctrl2(); }

    Unit ctrl(){
        await diff level <= 3 & drain <= 0 ;
        drain =  1/2;
        this!ctrl();
    }

    Unit ctrl2(){
        await diff level >= 10 & drain >= 0;
        drain = -1/2;
        this!ctrl2();

    }

    Real getDrain(){
        return drain;
    }
}

{
	new CSingleTank(5);
}
